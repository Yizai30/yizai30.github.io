# 引用
[虚幻引擎编译系统总结](https://zhuanlan.zhihu.com/c_1263606924853518337) by 雪流星
# 介绍
学习方法：参考作者大大的博客、问AI
# 使用工具
- Windows11
- UE5.5.4
- Visual Studio 2022
# UBT
解决方案里可以找到 UnrealBuildTool 项目的源码。

![alt text](/assets/img/2025-12-03-Unreal引擎编译系统总结/image.png)

比 UE4.26 多了 Artifacts、BatchFiles、BuildConfiguration、Preprocessor 几个文件夹，少了 app.config 文件，多了 .ubtignore 和 runtimeconfig.template.json 两个文件。

![alt text](/assets/img/2025-12-03-Unreal引擎编译系统总结/image1.png)

按照指引找到 Build.bat 文件。

```bat
@echo off

rem ## Unreal Engine code build script
rem ## Copyright Epic Games, Inc. All Rights Reserved.
rem ##
rem ## This script is expecting to exist in the /Engine/Build/BatchFiles directory.  It will not work correctly
rem ## if you copy it to a different location and run it.
rem ##
rem ##     %1 is the game name
rem ##     %2 is the platform name
rem ##     %3 is the configuration name
rem ##     additional args are passed directly to UnrealBuildTool

setlocal enabledelayedexpansion

rem ## First, make sure the batch file exists in the folder we expect it to.  This is necessary in order to
rem ## verify that our relative path to the /Engine/Source directory is correct
if not exist "%~dp0..\..\Source" goto Error_BatchFileInWrongLocation

rem ## Change the CWD to /Engine/Source.  We always need to run UnrealBuildTool from /Engine/Source!
pushd "%~dp0\..\..\Source"
if not exist ..\Build\BatchFiles\Build.bat goto Error_BatchFileInWrongLocation

set UBTPath="..\..\Engine\Binaries\DotNET\UnrealBuildTool\UnrealBuildTool.dll"

rem ## Verify that dotnet is present
call "%~dp0GetDotnetPath.bat"
if errorlevel 1 goto Error_NoDotnetSDK
REM ## Skip msbuild detection if using dotnet as this is done for us by dotnet-cli

rem ## Compile UBT if the project file exists
:ReadyToBuildUBT
set ProjectFile="Programs\UnrealBuildTool\UnrealBuildTool.csproj"
if not exist %ProjectFile% goto NoProjectFile

rem ## Only build if UnrealBuildTool.dll is missing, as Visual Studio or GenerateProjectFiles should be building UnrealBuildTool
rem ## Note: It is possible UnrealBuildTool will be out of date if the solution was generated with -NoDotNet or is VS2019
rem ##       Historically this batch file did not compile UnrealBuildTool
if not exist %UBTPath% (
	rem ## If this script was called from Visual Studio 2022, build UBT with Visual Studio to prevent unnecessary rebuilds.
	if "%VisualStudioVersion%" GEQ "17.0" (
		echo Building UnrealBuildTool with %VisualStudioEdition%...
		"%VSAPPIDDIR%..\..\MSBuild\Current\Bin\MSBuild.exe" %ProjectFile% -t:Build -p:Configuration=Development -verbosity:quiet -noLogo
		if errorlevel 1 goto Error_UBTCompileFailed
	) else (
		echo Building UnrealBuildTool with dotnet...
		dotnet build %ProjectFile% -c Development -v quiet
		if errorlevel 1 goto Error_UBTCompileFailed
	)
)
:NoProjectFile

rem ## Run UBT
:ReadyToBuild
if not exist %UBTPath% goto Error_UBTMissing
echo Running UnrealBuildTool: dotnet %UBTPath% %*
dotnet %UBTPath% %*
EXIT /B !ERRORLEVEL!

:Error_BatchFileInWrongLocation
echo ERROR: The batch file does not appear to be located in the Engine/Build/BatchFiles directory. This script must be run from within that directory.
EXIT /B 999

:Error_NoDotnetSDK
echo ERROR: Unable to find an install of Dotnet SDK.  Please make sure you have it installed and that `dotnet` is a globally available command.
EXIT /B 999

:Error_UBTCompileFailed
echo ERROR: Failed to build UnrealBuildTool.
EXIT /B 999

:Error_UBTMissing
echo ERROR: UnrealBuildTool.dll not found in %UBTPath%
EXIT /B 999
```

经 AI 分析 UE4.26 和 UE5.5.4 的 Build.bat 文件，得出：
1. 技术架构变化
  - UE4.26: 使用独立的UnrealBuildTool.exe
  - UE5.5: 使用UnrealBuildTool.dll + dotnet运行时

2. 复杂度和健壮性
  - UE4.26: 非常简单，只有25行，直接查找并执行UBT
  - UE5.5: 复杂得多，75行，包含完整的编译逻辑

3. 编译能力
  - UE4.26: 假设UBT已经存在，不处理编译
  - UE5.5: 包含完整的UBT编译逻辑，支持Visual Studio和dotnet两种编译方式

4. 错误处理
  - UE4.26: 基本的错误检查，简单的文件存在验证
  - UE5.5: 详细的错误处理，包括位置验证、.NET SDK检查、编译失败处理等

| 特性    | UE4.26 Build.bat | UE5.5 Build.bat |
|-------|------------------|-----------------|
| 文件大小  | 25行              | 75行             |
| UBT格式 | .exe             | .dll + dotnet   |
| 编译能力  | 无                | 完整编译逻辑          |
| 构建环境  | 简单执行             | 支持VS2022和dotnet |
| 错误处理  | 基础检查             | 全面验证            |
| 路径验证  | 无                | 严格的位置验证         |
| 依赖检查  | 无                | .NET SDK检查      |

跟据“生成命令行”执行的命令：
```shell
...\Build.bat AnimInstance_DemoEditor Win64 Development -Project="D:\Unreal\Experiment\AnimInstance_Demo\AnimInstance_Demo.uproject" -WaitMutex -FromMsBuild -architecture=x64
```

和 Build.bat 文件的注释：
```bat
rem ##     %1 is the game name
rem ##     %2 is the platform name
rem ##     %3 is the configuration name
rem ##     additional args are passed directly to UnrealBuildTool
```

可以得到：
- **game name**: AnimInstance_DemoEditor
- **platform name（活动解决方案平台）**: Win64（其它还有Win64-arm64、Win64-arm64ec）
- **configuration name（活动解决方案配置）**: Development（其它还有DebugGame、DebugGame Editor、Development Editor）
- **additional args**: -Project="D:\Unreal\Experiment\AnimInstance_Demo\AnimInstance_Demo.uproject" -WaitMutex -FromMsBuild -architecture=x64

# 坑1
在 Visual Studio 解决方案里改动文件位置，或删除文件，在实际的文件系统中并没有对应的改动。需要手动移动或删除文件系统上的文件，重新 **Generate Project Files** 。

# UHT
跟据反射属性标签（USTRUCT等），生成 .generated.h、.gen.cpp 文件，用于给蓝图暴露该类的信息。
![alt text](/assets/img/2025-12-03-Unreal引擎编译系统总结/image-1.png)

UCLASS默认是NotBlueprintable，除非继承自其它的标签。
![alt text](/assets/img/2025-12-03-Unreal引擎编译系统总结/image-2.png)

Actor类默认是Blueprintable的。
![alt text](/assets/img/2025-12-03-Unreal引擎编译系统总结/image-3.png)

各种GENERATED_BODY的宏定义。
![alt text](/assets/img/2025-12-03-Unreal引擎编译系统总结/image-4.png)

# 创建模块
手动创建模块文件夹，并手动创建 MyTestModule.build.cs、MyTestModule.cpp、MyTestModule.h。重新 **Generate Project Files** 。Private 和 Public 文件夹并没有被 .sln 加载到项目中。
![alt text](/assets/img/2025-12-03-Unreal引擎编译系统总结/image-5.png)

（未完）